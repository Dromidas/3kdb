#alias common_corpse kill;
#class common_corpse open;

#alias formatItem {
    #format result {<acf>%+10s  %s<088>} {%1} {%2};
    #echo { $result};
};

#alias corpsetrig {
    #if {"%0" == "clear"} {
        #list corpseActions clear;
        #echo {<caa>----  <fff> corpsetrig has been cleared <caa>  ----<088>};
    } {
        #if {"%0" != ""} {
            #list corpseList {find} {%0} {tempListIndex};
            #var tempList $corpseList[+$tempListIndex];
            #if {&tempList[] == 0} {
                #list corpseActions indexate name;
                #list corpseActions {find} {%0} {tempListIndex};
                #var tempList $corpseList[+$tempListIndex];
            };
            #if {&tempList[] > 0} {
                #NOP -- If the corpseActions list exists, check if the selected action is a member and toggle it;
                #NOP -- Otherwise, if there is no corpseActions list, we add it;
                #if {&corpseActions[] > 0} {
                    #list corpseActions indexate name;
                    #list corpseActions {find} {%0} {itemExists};
                    #if {$itemExists} {
                        #echo {<caa>----  <fff> %0 has been <baa>removed<088> from <fff>corpse_trig <caa>  ----<088>};
                        #list corpseActions filter {} {%0};
                    } {
                        #list corpseActions add {{{name} {$tempList[name]} {action} {$tempList[action]} {active} {1}}};
                        #echo {<caa>----  <fff> %0 has been <bfd>added<088> to <fff>corpse_trig  <caa>  ----<088>};
                    };
                } {
                    #list corpseActions add {{{name} {$tempList[name]} {action} {$tempList[action]} {active} {1}}};
                    #echo {<caa>----  <fff> %0 has been <bfd>added<088> to <fff>corpse_trig  <caa>  ----<088>};
                };
            } {
                #echo { <caa>----  <fff> %0 not found in corpsetrig List.  <caa>  ----<088>};
                #echo { <caa>----  <fff> %0 has been added as an action.  <caa>  ----<088>};
                #list corpseActions add {{{name} {%0} {action} {%0} {active} {1}}};
            };
        } {
            #echo {<ffb>   Current Corpse Trigger Actions   <088>\n};
            #if {&corpseActions[] > 0} {
                formatItem {Priority:} {Name};
                #loop 1 &corpseActions[] item {
                    formatItem {<afc>$item:} {<efe>$corpseActions[$item][name]<088>};
                };
                #echo { \n<efa>corpsetrig++<088> <bcc>name<088> to <efc>increase<088> an action's priority};
                #echo { <efa>corpsetrig--<088> <bcc>name<088> to <fac>decrease<088> an action's priority};
                #echo { \n<efa>corpsetrig<088> <bcc>name<088> to add or remove an action};
                #echo { <efa>corpsetrig-all<088> to see available actions};
                #echo { <efa>corpsetrig clear<088> to clear corpsetrig};
            } {
                #echo { There are currently no actions added to corpse_trig};
                #echo { \n<efa>corpsetrig<088> <bcc>name<088> to add or remove an action};
                #echo { <efa>corpsetrig-all<088> to see available actions};
                #echo { If the action doesn't exist, it will be added};
            };
        };
    };
};


#alias corpsetrig++ {
    #if {"%0" != ""} {
        #var foundItem 0;
        #list corpseActions indexate name;
        #list corpseActions find {%0} {foundItem};
        #if {$foundItem == 0} {
            #echo { <bba>$foundItem[name] was not found in the corpsetrig actions<088>};
        };
        #if {$foundItem != 0 && $foundItem > 1} {
            #math swapItem {$foundItem - 1};
            #list corpseActions swap $foundItem $swapItem;
            #echo { <efc>$foundItem[name] priority has been increased by 1<088>};
        } {
            #echo { <bba>$foundItem[name] priority is already the first item to trigger<088>};
        };
    } {
        #echo {<fac>  No corpsetrig name provided.<088>};
    };
};


#alias corpsetrig-- {
    #if {"%0" != ""} {
        #var foundItem 0;
        #list corpseActions indexate name;
        #list corpseActions find {%0} {foundItem};
        #if {$foundItem == 0} {
            #echo { <bba>$foundItem[name] was not found in the corpsetrig actions<088>};
        };
        #if {$foundItem != &corpseActions[]} {
            #if {$foundItem != 0 && $foundItem >= 1} {
                #math swapItem {$foundItem + 1};
                #list corpseActions swap $foundItem $swapItem;
                #echo { <efc>$foundItem[name] priority has been decreased by 1<088>};
            };
        } {
            #echo { <bba>$corpseActions[$foundItem][name] priority is already the last item<088>};
        };
    } {
        #echo {<fac>  No corpsetrig name provided.<088>};
    };
};


#alias {corpsetrig-all} {
    #echo {\n<ffb>   Current Corpse Trigger Actions   <088>};
    #if {&corpseActions[] > 0} {
        #loop 1 &corpseActions[] item {
            #echo {   <afc>$item: <efe> $corpseActions[$item][name]   <088>};
        };
    } {
        #echo {<fac>             None              <088>};
    };
    #var inactiveItems ${corpseList};
    #list inactiveItems indexate name;
    #if {&corpseActions[] > 0} {
        #foreach $corpseActions[%*] active {
            #list inactiveItems filter {} {$active[name]};
        };
    };
    #if {&inactiveItems[] > 0} {
        #echo {\n<ffb>   Available Options   <088>};
        formatItem {Name} {Description};
        #foreach $inactiveItems[%*] index {
            #if {"$index[guild]" == "all" || "$index[guild]" == "$guild"} {
                formatItem {$index[name]} {<cba>$index[description]<088>};
            };
        };
    };
    #echo { \n<efa>corpsetrig<088> <bcc>action<088> to add or remove an action};
    #echo { <efa>corpsetrig-all<088> to see available actions};
    #echo { If the action doesn't exist, it will be added};
};


#list corpseList clear;
#list corpseList add {{{name} {aoe} {guild} {all} {action} {immolate corpse} {active} {0} {description} {AOE Armor: Immolate Corpse}}};
#list corpseList add {{{name} {divvy} {guild} {all} {action} {divvy gold} {active} {0} {description} {Divvy gold}}};
#list corpseList add {{{name} {pause} {guild} {all} {action} {#delay 2 {#NOP}} {active} {0} {description} {2 Second Pause}}};
#list corpseList add {{{name} {wrap} {guild} {all} {action} {wrap remains}  {active} {0} {description} {Coffin: Wrap Corpse}}};
#list corpseList add {{{name} {slab} {guild} {all} {action} {slab remains}  {active} {0} {description} {Freezer: Slab Corpse}}};
#list corpseList add {{{name} {dirge} {guild} {bard} {action} {perform dirge}  {active} {0} {description} {Bard: Perform Dirge}}};
#list corpseList add {{{name} {deener} {guild} {juggernaut} {action} {deener}  {active} {0} {description} {Juggernaut: Deener Drain Stim}}};
#list corpseList add {{{name} {fdeener} {guild} {juggernaut} {action} {fdeener}  {active} {0} {description} {Juggernaut: Fdeener Drain Stim}}};
#list corpseList add {{{name} {edrain} {guild} {elemental} {action} {evoke energy drain}  {active} {0} {description} {Elemental: Energy Drain}}};
#list corpseList add {{{name} {preserve} {guild} {necromancer} {action} {qtrance preserve remains/get remains}  {active} {0} {description} {Necro: Preserve Remains}}};
#list corpseList indexate name;


#alias corpse_trig {
    #if {&corpseActions[] > 0} {
        #foreach $corpseActions[%*] item {
            $item[action];
        };
    };
};


#class common_corpse close;